---
title: NixOS
description: Start here to set up and use Noctalia.
sidebar:
  order: 15
---

import SettingsReference from '../../../components/SettingsReference.astro';


For NixOS users, you can add Noctalia to your system configuration using flakes and the provided NixOS module.

:::note
The Noctalia flake requires **nixpkgs unstable**, since we depend on the latest Quickshell version.
:::

## Installation

To install Noctalia, you need to [add a flake input](#add-flake-input) and [install the package](#install-package).

:::caution
To make Noctalia's wifi, bluetooth, power-profile, and battery feature available, please ensure the following NixOS options are enabled:
- `networking.networkmanager.enable`
- `hardware.bluetooth.enable`
- `services.power-profiles-daemon.enable` or `services.tuned.enable`
- `services.upower.enable`
:::

### Adding the flake input {#add-flake-input}

Add the required inputs to your flake configuration:

```nix
# flake.nix
{
  description = "NixOS configuration with Noctalia";

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";

    noctalia = {
      url = "github:noctalia-dev/noctalia-shell";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = inputs@{ self, nixpkgs, ... }: {
    nixosConfigurations.awesomebox = nixpkgs.lib.nixosSystem {
      specialArgs = { inherit inputs; };
      modules = [
        # ... other modules
        ./noctalia.nix
      ];
    };
  };
}
```

With the flake installed, we can access Noctalia packages and modules in our configuration.

### Installing the package {#install-package}

If you don't use Noctalia's NixOS or home-manager modules, you need to manually install the default Noctalia package from the new `noctalia` input:

```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  # install package
  environment.systemPackages = with pkgs; [
    inputs.noctalia.packages.${pkgs.stdenv.hostPlatform.system}.default
    # ... maybe other stuff
  ];
}
```

Rebuild with `sudo nixos-rebuild switch --flake .`.

---

## Config with home-manager

If you have [home manager](https://github.com/nix-community/home-manager) installed, you can configure your Noctalia shell in Nix.

### Noctalia settings

To configure Noctalia settings, you can use the Noctalia home manager module:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # import the home manager module
    imports = [
      inputs.noctalia.homeModules.default
    ];

    # configure options
    programs.noctalia-shell = {
      enable = true;
      settings = {
        # configure noctalia here
        bar = {
          density = "compact";
          position = "right";
          showCapsule = false;
          widgets = {
            left = [
              {
                id = "ControlCenter";
                useDistroLogo = true;
              }
              {
                id = "WiFi";
              }
              {
                id = "Bluetooth";
              }
            ];
            center = [
              {
                hideUnoccupied = false;
                id = "Workspace";
                labelMode = "none";
              }
            ];
            right = [
              {
                alwaysShowPercentage = false;
                id = "Battery";
                warningThreshold = 30;
              }
              {
                formatHorizontal = "HH:mm";
                formatVertical = "HH mm";
                id = "Clock";
                useMonospacedFont = true;
                usePrimaryColor = true;
              }
            ];
          };
        };
        colorSchemes.predefinedScheme = "Monochrome";
        general = {
          avatarImage = "/home/drfoobar/.face";
          radiusRatio = 0.2;
        };
        location = {
          monthBeforeDay = true;
          name = "Marseille, France";
        };
      };
      # this may also be a string or a path to a JSON file,
      # but in this case must include *all* settings.
    };
  };
}
```



We don't have extensive documentation of Noctalia's configuration options; however, you can see an up-to-date list of [configuration defaults](#config-ref).

Additionally, if you use the [systemd service](#systemd-service), editing the settings with the GUI will show the resulting configuration in `~/.config/noctalia/gui-settings.json`. You can then use this information to update your Nix config to make it permanent.

:::tip
 To see the diff between your current build and the temporary GUI settings, run:
```sh
nix shell nixpkgs#jq nixpkgs#colordiff -c bash -c "diff -u <(jq -S . ~/.config/noctalia/settings.json) <(jq -S . ~/.config/noctalia/gui-settings.json) | colordiff"
```
:::


### Theme colors


To configure Noctalia settings, you can use the Noctalia home manager module. Be sure to set every Material 3 color, including hover-state values, when overriding the defaults:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # import the home manager module
    imports = [
      inputs.noctalia.homeModules.default
    ];

    # configure options
    programs.noctalia-shell = {
      enable = true;
      colors = {
        # you must set ALL of these
        mError = "#dddddd";
        mOnError = "#111111";
        mOnPrimary = "#111111";
        mOnSecondary = "#111111";
        mOnSurface = "#828282";
        mOnSurfaceVariant = "#5d5d5d";
        mOnTertiary = "#111111";
        mOnHover = "#ffffff";
        mOutline = "#3c3c3c";
        mPrimary = "#aaaaaa";
        mSecondary = "#a7a7a7";
        mShadow = "#000000";
        mSurface = "#111111";
        mHover = "#1f1f1f";
        mSurfaceVariant = "#191919";
        mTertiary = "#cccccc";
      };
      # this may also be a string or a path to a JSON file.
    };
  };
}
```

### User Templates

The `user-templates` option allows you to define custom [Matugen](https://github.com/InioX/matugen) templates.
This is useful for theming other applications that are not supported by default.

It can be a string, a path, or an attribute set.

As a string:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # ...
    programs.noctalia-shell = {
      enable = true;
      settings = {
        templates = {
          enableUserTemplates = true;
        };
      };
      user-templates = ''
        [config]
        # General Matugen config settings
        [templates.myapp]
        input_path = "~/.config/noctalia/templates/myapp.css"
        output_path = "~/.config/myapp/theme.css"
        post_hook = "myapp --reload-theme"
      '';
    };
  };
}
```

As a path:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # ...
    programs.noctalia-shell = {
      enable = true;
      settings = {
        templates = {
          enableUserTemplates = true;
        };
      };
      user-templates = ./path/to/user-templates.toml;
    };
  };
}
```

As an attribute set:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # ...
    programs.noctalia-shell = {
      enable = true;
      settings = {
        templates = {
          enableUserTemplates = true;
        };
      };
      user-templates = {
        config = {
          # General Matugen config settings
        };
        templates = {
          myapp = {
            input_path = "~/.config/noctalia/templates/myapp.css";
            output_path = "~/.config/myapp/theme.css";
            post_hook = "myapp --reload-theme";
          };
        };
      };
    };
  };
}
```

### Wallpapers

Wallpapers are configured in `~/.cache/noctalia/wallpapers.json`. You can set them declaratively:

```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # ... other config ...
    
    home.file.".cache/noctalia/wallpapers.json" = {
      text = builtins.toJSON {
        defaultWallpaper = "/path/to/default/wallpaper.png";
        wallpapers = {
          "DP-1" = "/path/to/monitor/wallpaper.png";
        };
      };
    };
  };
}
```

### Keybinds

Keybindings are configured in your compositor. You make IPC calls to Noctalia [as explained in the keybinds section](/getting-started/keybinds). The only difference is that you can call `noctalia-shell` instead of `qs -c noctalia-shell`.

To safely configure noctalia keybindings in the Niri flake, be sure to pass a list:
```nix
# niri.nix
{ pkgs, inputs, ... }:
{
  # ...
  home-manager.users.drfoobar =
    { config, ... }:
    {
      # ...
      programs.niri = {
        settings = {
          # ...
          binds = with config.lib.niri.actions; {
            "Mod+Space".action.spawn = [
              "noctalia-shell" "ipc" "call" "launcher" "toggle" # ✅
            ];
            "Mod+P".action.spawn =
              "noctalia-shell ipc call sessionMenu toggle"; # ❌
        };
      };
    };
}

```

It may be convenient to define a utility function to allow you to use simple strings:

```nix
# niri.nix
{ pkgs, inputs, ... }:

let
  noctalia = cmd: [
    "noctalia-shell" "ipc" "call"
  ] ++ (pkgs.lib.splitString " " cmd);
in
{
  # ...
  home-manager.users.drfoobar =
    { config, lib, ... }:
    {
      # ...
      programs = {
        niri = {
          # ...
          settings = {
            binds = with config.lib.niri.actions; {
              # ...
              "Mod+L".action.spawn = noctalia "lockScreen lock";
              "XF86AudioLowerVolume".action.spawn = noctalia "volume decrease";
              "XF86AudioRaiseVolume".action.spawn = noctalia "volume increase";
              "XF86AudioMute".action.spawn = noctalia "volume muteOutput";
              # etc
          };
        };
      };
    };
}

```
---

## Running the shell

The Noctalia shell can now be run in two ways: [manually](#running-manually) or using a systemd service


### Running manually {#running-manually}

To run Noctalia using the flake, run:

```bash
noctalia-shell
```

With the [Niri flake](https://github.com/sodiboo/niri-flake), you can then spawn Noctalia on startup like this:

```nix
# niri.nix
{ pkgs, inputs, ... }:
{
  # ...

  home-manager.users.drfoobar = {
    # ...
    programs.niri = {
      package = niri;
      settings = {
        # ...
        spawn-at-startup = [
          {
            command = [
              "noctalia-shell"
            ];
          }
        ];
      };
    };
  };
}

```

### Running with systemd {#systemd-service}

You can set up systemd service via NixOS module or Home Manager module. If you [configure Noctalia shell with home-manager](#config-with-home-manager), then Home Manager module is a better choice.
It's best not to enable systemd service in both modules at the same time, as names of two systemd services are the same; it's unpredictable which one will run (usually, it's the systemd service in Home Manager module).

:::caution
The systemd service is currently **experimental**:
- you may experience weird issues when rebuilding your system
- We haven't fully tested newer features on **Hyprland**

Please send us feedback on the [#nixos channel](https://discord.com/channels/1401598189823590460/1418903722616160349) on Discord or [raise an issue](https://github.com/noctalia-dev/noctalia-shell/issues) if you encounter issues.
:::

#### NixOS module

Import the Noctalia NixOS modules and enable the service:

```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  # import the nixos module
  imports = [
    inputs.noctalia.nixosModules.default
  ];
  # enable the systemd service
  services.noctalia-shell.enable = true;
}
```

The service will start after [`graphical-session.target`](https://www.freedesktop.org/software/systemd/man/latest/systemd.special.html#graphical-session.target) by default, which will work correctly out of the box for [Niri](https://github.com/YaLTeR/niri) and [Hyprland with UWSM](https://wiki.hypr.land/Useful-Utilities/Systemd-start/#uwsm).

If you have some custom target, you can configure it with the `target` option:
```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  # ...
  services.noctalia-shell = {
    enable = true;
    target = "my-hyprland-session.target";
  };
}
```
This target depends on how you configure your Hyprland session systemd service.

:::caution
If you also [configure Noctalia shell with home-manager](#config-with-home-manager), please make sure to set the `programs.noctalia-shell.package` option in home-manager module to null (otherwise it may cause IPC commands to fail).
:::

#### Home Manager module

Import the Noctalia Home Manager modules and enable the service:

```nix
# noctalia.nix
{ pkgs, inputs, ... }:
{
  home-manager.users.drfoobar = {
    # import the home manager module
    imports = [
      inputs.noctalia.homeModules.default
    ];

    # enable the systemd service
    programs.noctalia-shell.systemd.enable = true;
  };
}
```
The systemd service target is set by the `wayland.systemd.target` option in Home Manager.

:::caution
When switching home manager generations, settings and color file backups may cause errors like
`Existing file '/home/myuser/.config/noctalia/colors.json.backup' would be clobbered by backing up
 '/home/myuser/.config/noctalia/colors.json'`; if this happens you need to delete or move the files in
this directory and rebuild.
:::

---

## Calendar events support

To add support for events in the calendar via evolution-data-server.

```nix
# noctalia.nix
{ pkgs, inputs, lib, ... }:
{
  services.gnome.evolution-data-server.enable = true;

  environment.systemPackages = with pkgs; [
    (python3.withPackages (pyPkgs: with pyPkgs; [ pygobject3 ]))
  ];

  environment.sessionVariables = {
    GI_TYPELIB_PATH = lib.makeSearchPath "lib/girepository-1.0" (
      with pkgs;
      [
        evolution-data-server
        libical
        glib.out
        libsoup_3
        json-glib
        gobject-introspection
      ]
    );
  };
}
```

---

## Configuration Defaults {#config-ref}

There is no comprehensive documentation of all configuration options, but the default values are autogenerated and placed in a [`settings-default.json`](https://github.com/noctalia-dev/noctalia-shell/blob/main/Assets/settings-default.json) in the Noctalia shell repository. We retrieve a live Nixified version of these here for your convenience:

<SettingsReference />

